pkgname=spl
pkgver=0.6.0_rc9
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="Solaris Porting Layer"
_kernver=`pacman -Q linux | cut -d " " -f 2 | cut -d "-" -f 1`
makedepends=("linux-headers=${_kernver}")
depends=("linux=${_kernver}" "spl-utils=${pkgver}")
url="http://zfsonlinux.org/"
source=(http://github.com/downloads/zfsonlinux/spl/spl-${pkgver//_/-}.tar.gz preempt.patch)
md5sums=('82466940669583234e90c8a47b5c6cd8'
         '87315d5e862024760d0088a806463813')
install=spl.install
license=('GPL')

build() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  patch -p1 -i $srcdir/preempt.patch
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib --with-config=kernel
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  make DESTDIR="$pkgdir" install

  # move module tree /lib -> /usr/lib
  cp -r "$pkgdir/lib" "$pkgdir/usr"
  rm -r "$pkgdir/lib"
}
