pkgname=zfs
pkgver=0.6.0_rc9
pkgrel=1
arch=('i686' 'x86_64')
pkgdesc="Native ZFS for Linux"
url="http://zfsonlinux.org/"
depends=('util-linux' "spl=$pkgver-$pkgrel")
makedepends=('linux-headers')
source=(http://github.com/downloads/zfsonlinux/zfs/zfs-${pkgver//_/-}.tar.gz
        preempt.patch
        zfs.initcpio.hook
        zfs.initcpio.install)
md5sums=('f41082bd7073198cabf37e6e54359a07'
         '9a5dc8b83c6b523fd2bd0be842278e2c'
         'ca13cbbfa056847aa5a3a1479b43a066'
         'af4deb337da54312591dbbc9b6a9380e')
license=('CDDL')
install=zfs.install

build() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  patch -p1 -i $srcdir/preempt.patch
  ./autogen.sh
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  make DESTDIR="$pkgdir" install
  install -D -m644 zfs.initcpio.hook "$pkgdir"/lib/initcpio/hooks/zfs
  install -D -m644 zfs.initcpio.install "$pkgdir"/lib/initcpio/install/zfs
}
