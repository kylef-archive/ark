pkgname=znc
pkgver=0.2_beta1
pkgrel=1
pkgdesc='An IRC bouncer with modules & scripts support'
url='http://wiki.znc.in/'
license=('GPL2')
arch=('i686' 'x86_64')
source=(http://people.znc.in/~psychon/znc/releases/znc-${pkgver//_/-}.tar.gz)
md5sums=('fc93f5a66ace23aa6d8b289a50213008')

depends=('c-ares' 'gcc-libs' 'openssl')
makedepends=('swig' 'tcl' 'python' 'perl' 'cyrus-sasl')
optdepends=('tcl: modtcl module'
            'python: modpython module'
            'perl: modperl module'
            'cyrus-sasl: saslauth module')

build() {
  cd "$srcdir/znc-${pkgver//_/-}"
  ./autogen.sh
  ./configure --prefix=/usr \
    --enable-sasl \
    --enable-tcl \
    --enable-python \
    --enable-perl \
    --enable-extra
  make
}

package() {
  cd "$srcdir/znc-${pkgver//_/-}"
  make DESTDIR="$pkgdir" install
}
