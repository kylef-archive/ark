pkgname='nss-pam-ldapd'
pkgver='0.8.10'
pkgrel='1'
pkgdesc='LDAP module for NSS and PAM'
arch=('i686' 'x86_64')
url="http://arthurdejong.org/nss-pam-ldapd/"
license=('LGPL2.1')
depends=('pam' 'libldap' 'cyrus-sasl' 'krb5')
conflicts=('nss_ldap' 'pam_ldap')
replaces=('nss_ldap' 'pam_ldap')

backup=('etc/nslcd.conf')
install=nslcd.install

sha1sums=('4dd50c0537a13f454625e0325b8c863e5adb48a8'
          '56739b0084442bd151e775ed3e03836ff2e4c400')
source=("$url$pkgname-$pkgver.tar.gz" nslcd.rc.d)

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure --prefix='/usr' \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --enable-sasl \
        --with-pam-seclib-dir=/usr/lib/security \
        --with-nslcd-pidfile=/var/run/nslcd.pid \
        --with-nslcd-socket=/var/run/nslcd.sock
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="$pkgdir" install
    install -Dm755 $srcdir/nslcd.rc.d $pkgdir/etc/rc.d/nslcd
}
