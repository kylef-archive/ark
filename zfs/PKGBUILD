pkgname=zfs
pkgver=0.6.0_rc9
pkgrel=3
arch=('i686' 'x86_64')
pkgdesc="Native ZFS for Linux"
url="http://zfsonlinux.org/"
_kernver=`pacman -Q linux | cut -d " " -f 2 | cut -d "-" -f 1`
depends=("linux=${_kernver}" "zfs-utils=${pkgver}" 'util-linux' "spl=$pkgver")
makedepends=("linux-headers=${_kernver}")
source=(http://github.com/downloads/zfsonlinux/zfs/zfs-${pkgver//_/-}.tar.gz
        preempt.patch)
md5sums=('f41082bd7073198cabf37e6e54359a07'
         '9a5dc8b83c6b523fd2bd0be842278e2c')
license=('CDDL')
install=zfs.install

build() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  patch -p1 -i $srcdir/preempt.patch
  ./autogen.sh
  ./configure --prefix=/usr --libdir=/usr/lib --sysconfdir=/etc --with-config=kernel
  make
}

package() {
  cd "$srcdir/$pkgname-${pkgver//_/-}"
  make DESTDIR="$pkgdir" install

  # move module tree /lib -> /usr/lib
  cp -r "$pkgdir/lib" "$pkgdir/usr"
  rm -r "$pkgdir/lib"
}
