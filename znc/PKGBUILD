pkgname=znc
pkgver=1.0_p1
pkgrel=1
pkgdesc='An IRC bouncer with modules & scripts support'
url='http://wiki.znc.in/'
license=('GPL2')
arch=('i686' 'x86_64')
source=(http://znc.sector5d.org/znc-${pkgver//_/-}.tar.gz)
sha1sums=('127a9efab4536bba27e850ee0aae42d098bcb398')

depends=('c-ares' 'gcc-libs' 'openssl')
makedepends=('swig' 'tcl' 'python' 'perl' 'cyrus-sasl')
optdepends=('tcl: modtcl module'
            'python: modpython module'
            'perl: modperl module'
            'cyrus-sasl: saslauth module')

build() {
  cd "$srcdir/znc-${pkgver//_/-}"
  ./autogen.sh
  ./configure --prefix=/usr \
    --enable-sasl \
    --enable-tcl \
    --enable-python \
    --enable-perl \
    --enable-extra
  make
}

package() {
  cd "$srcdir/znc-${pkgver//_/-}"
  make DESTDIR="$pkgdir" install
}
