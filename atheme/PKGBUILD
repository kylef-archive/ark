pkgname=atheme
pkgver=7.0.5
pkgrel=1
pkgdesc='A portable and secure set of open-source and modular IRC services'
url='http://atheme.net/'
license=('ISC')
arch=('x86_64' 'i686')
depends=('libmowgli>=2.0.0' 'openssl' 'pcre' 'gettext')
makedepends=('perl' 'cracklib')
optdepends=('libldap: auth/ldap module'
            'cracklib: nickserv/cracklib module'
            'perl: scripting/perl')
install=atheme.install

source=(http://www.atheme.net/downloads/atheme-services-$pkgver.tar.bz2
        atheme.rcd atheme.service atheme-rundir-to-varlib.patch)
sha1sums=('9f83ece548d5868092498c38ddc793de7beb0f07'
          'c5f8bfa66eb4d0e90f74fbb63d7f4fc50e587211'
          'f360f3515f12eedb198d8abedffd9d302dab2a22'
          'e1528a425048fdbe693ff7572b71c3535ef39a41')

build() {
  cd "${srcdir}/atheme-services-${pkgver}"

  patch -Np1 -i "${srcdir}/atheme-rundir-to-varlib.patch"

  ./configure --enable-fhs-paths \
    --prefix=/usr \
    --sysconfdir=/etc/atheme \
    --localstatedir=/var \
    --docdir=/usr/share/doc/atheme \
    --with-cracklib \
    --with-perl \
    --with-pcre \
    --enable-contrib
  make
}

package() {
  install -Dm755 "${srcdir}"/atheme.rcd "${pkgdir}"/etc/rc.d/atheme
  install -Dm644 "${srcdir}"/atheme.service "${pkgdir}"/usr/lib/systemd/system/atheme.service

  cd "${srcdir}/atheme-services-${pkgver}"
  make DESTDIR="$pkgdir" install
  chown 142:142 $pkgdir/var/{lib,log}/atheme
  install -Dm 644 $pkgdir/usr/share/doc/atheme/LICENSE $pkgdir/usr/share/licenses/atheme/LICENSE
}
