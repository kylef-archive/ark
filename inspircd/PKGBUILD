pkgname=inspircd
pkgver=2.0.7
pkgrel=2
pkgdesc='A lightweight IRC daemon'
arch=('x86_64' 'i686')
url='http://www.inspircd.org/'
license=('GPL2')
depends=('perl' 'gnutls' 'openssl' 'libgcrypt')
makedepends=('pkg-config' 'mysql' 'postgresql' 'sqlite3' 'libldap' 'geoip' 'tre')
optdepends=('mysql: m_mysql'
            'pcre: m_regex_pcre'
            'postgresql: m_pgsql'
            'sqlite3: m_sqlite3'
            'libldap: m_ldapoper and m_ldapauth'
            'geoip: m_geoip'
            'tre: m_regex_tre')
install=inspircd.install
backup=('etc/inspircd/inspircd.conf')

source=(https://github.com/downloads/inspircd/inspircd/InspIRCd-$pkgver.tar.bz2
        inspircd.rcd)
sha1sums=('51d43e417dc1fee5f8325118c42a936df4d42466'
          '59c4aa6929732fb076ca2be96a01385cae9e8484')

build() {
  cd "${srcdir}/inspircd"

  ./configure \
    --enable-extras=m_geoip.cpp \
    --enable-extras=m_ldapauth.cpp \
    --enable-extras=m_ldapoper.cpp \
    --enable-extras=m_mysql.cpp \
    --enable-extras=m_pgsql.cpp \
    --enable-extras=m_regex_pcre.cpp \
    --enable-extras=m_regex_posix.cpp \
    --enable-extras=m_regex_tre.cpp \
    --enable-extras=m_sqlite3.cpp

  ./configure \
    --prefix=/usr/lib/inspircd \
    --binary-dir=/usr/sbin \
    --module-dir=/usr/lib/inspircd/modules \
    --config-dir=/etc/inspircd \
    --enable-gnutls \
    --enable-openssl \
    --enable-epoll

  make
}

package() {
  install -Dm755 "${srcdir}"/inspircd.rcd "${pkgdir}"/etc/rc.d/inspircd
  install -o141 -g141 -dm750 "${pkgdir}/var/log/inspircd"
  install -o141 -g141 -dm750 "${pkgdir}/var/run/inspircd"

  cd "${srcdir}/inspircd"
  make DESTDIR="$pkgdir" INSTUID=141 install

  rm -rf "${pkgdir}"/usr/lib/inspircd/logs
  rm -rf "${pkgdir}"/usr/lib/inspircd/data
}
